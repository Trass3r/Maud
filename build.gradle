plugins {
    id 'application'
    id 'java'
}

description = 'An editor for animated 3-D models.'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

ext.jmonkeyengineVersion = '3.4.0-beta4' // from mavenCentral

tasks.withType(JavaCompile) { // Java compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    //options.deprecation = true
    options.encoding = 'UTF-8'
}

tasks.withType(JavaExec) { // Java runtime options:
    //args '--forceDialog'
    //args '--openGL3'
    //args '--openGL33'
    //args '--skipStartup'
    //args '--verbose'
    classpath sourceSets.main.runtimeClasspath
    //debug true
    enableAssertions true
    //jvmArgs '-verbose:gc'
    //jvmArgs '-Xbatch'
    jvmArgs '-Xms4g', '-Xmx4g'
    //jvmArgs '-XX:+PrintCompilation'
    //jvmArgs '-XX:+UseConcMarkSweepGC'
    jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

mainClassName = 'maud.Maud'
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}
jar.manifest.attributes('Main-Class': mainClassName)

repositories {
    //mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' } // for jme3_xbuf_loader
}

dependencies {
    // from mavenCentral (or mavenLocal) repositories:
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    runtimeOnly    'com.github.nifty-gui:nifty-style-black:1.4.3'
    implementation 'com.github.stephengold:Heart:6.4.3+for34'
    implementation 'com.github.stephengold:jme3-utilities-ui:0.9.3+for34'
    implementation 'com.github.stephengold:jme3-utilities-nifty:0.9.19+for34'
    implementation 'com.github.stephengold:Minie:4.1.0+big3+for34'
    implementation 'com.github.stephengold:SkyControl:0.9.31+for34'
    implementation 'com.github.stephengold:Wes:0.6.3+for34'
    implementation 'org.jmonkeyengine:jme3-blender:3.3.2-stable'
    implementation 'org.jmonkeyengine:jme3-desktop:' + jmonkeyengineVersion
    implementation 'org.jmonkeyengine:jme3-lwjgl:' + jmonkeyengineVersion
    implementation 'org.jmonkeyengine:jme3-plugins:' + jmonkeyengineVersion
    //runtimeOnly 'org.jmonkeyengine:jme3-testdata:3.1.0-stable'

    implementation 'com.github.stephengold:jme-ttf:2.2.3+for34'

    // from jitpack repositories:
    implementation 'com.github.xbuf.jme3_xbuf:jme3_xbuf_loader:0.9.1'
}

task runForceDialog(type: JavaExec) {
    args '--forceDialog'
    main 'maud.Maud'
}

// cleanup tasks
clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs', 'cleanWrittenAssets')
task cleanDLLs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dll')
}
task cleanDyLibs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dylib')
}
task cleanLogs(type: Delete) {
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
task cleanSOs(type: Delete) {
    delete fileTree(dir: '.', include: '*.so')
}
task cleanWrittenAssets(type: Delete) {
    delete 'Written Assets'
}
