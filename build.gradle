plugins {
    id 'application'
    id 'java'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) { // compile-time options:
        options.compilerArgs << '-Xdiags:verbose'
        options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
        options.encoding = 'UTF-8'
    }
    tasks.withType(JavaExec) { // runtime options:
        //args '--forceDialog'
        //args '--openGL3'
        //args '--openGL33'
        //args '--skipStartup'
        //args '--verbose'
        classpath sourceSets.main.runtimeClasspath
        //debug true
        enableAssertions true
        //jvmArgs '-verbose:gc'
        //jvmArgs '-Xbatch'
        jvmArgs '-Xms5g', '-Xmx5g'
        //jvmArgs '-XX:+PrintCompilation'
        jvmArgs '-XX:+UseConcMarkSweepGC'
        //jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
    }
}

ext.jmonkeyengineVersion = '3.2.4-stable' // from jcenter

mainClassName = 'maud.Maud'
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}
jar.manifest.attributes('Main-Class': mainClassName)

repositories {
    //mavenLocal()
    maven { url 'https://dl.bintray.com/stephengold/jme3utilities' }
    //maven { url 'https://dl.bintray.com/stephengold/org.jmonkeyengine' } // for jme3-testdata
    maven { url 'https://dl.bintray.com/riccardo/v-hacd' } // for v-hacd-java-bindings
    //maven { url 'https://dl.bintray.com/jmonkeyengine/contrib' }
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    // from stephengold's bintray repositories:
    implementation 'jme3utilities:jme3-utilities-heart:4.1.0for32'
    implementation 'jme3utilities:jme3-utilities-ui:0.7.8for32'
    implementation 'jme3utilities:jme3-utilities-nifty:0.9.10for32'
    implementation 'jme3utilities:Minie:1.1.0for32'
    implementation 'jme3utilities:SkyControl:0.9.22for32'
    implementation 'jme3utilities:Wes:0.4.5for32'
    //runtimeOnly 'org.jmonkeyengine:jme3-testdata:3.1.0-stable'

    // from jcenter (or mavenLocal) repositories:
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'com.atr:jme-ttf:2.2.2'
    runtimeOnly 'com.github.nifty-gui:nifty-style-black:1.4.3'
    implementation "org.jmonkeyengine:jme3-blender:$jmonkeyengineVersion"
    implementation "org.jmonkeyengine:jme3-desktop:$jmonkeyengineVersion"
    implementation "org.jmonkeyengine:jme3-lwjgl:$jmonkeyengineVersion"
    implementation "org.jmonkeyengine:jme3-plugins:$jmonkeyengineVersion"

    // from jitpack repositories:
    implementation 'com.github.xbuf.jme3_xbuf:jme3_xbuf_loader:0.9.1'
}

// cleanup tasks
clean.dependsOn('cleanDLLs', 'cleanLogs', 'cleanSOs', 'cleanWrittenAssets')
task cleanDLLs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dll')
}
task cleanLogs(type: Delete) {
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
task cleanSOs(type: Delete) {
    delete fileTree(dir: '.', include: '*.so')
}
task cleanWrittenAssets(type: Delete) {
    delete 'Written Assets'
}
