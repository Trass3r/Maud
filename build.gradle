apply plugin: 'java'

// select one source-code option
//sourceCompatibility = '1.7'
sourceCompatibility = '1.8'

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        // compile-time options:
        options.compilerArgs << '-Xdiags:verbose'
        options.compilerArgs << '-Xlint:unchecked'
        options.deprecation = true
        options.encoding = 'UTF-8'
    }
    tasks.withType(JavaExec) {
        // runtime options:
        //args '--forceDialog'
        //args '--skipStartup'
        classpath sourceSets.main.runtimeClasspath
        //doFirst { println commandLine }
        enableAssertions true
        jvmArgs '-XX:+UseConcMarkSweepGC'
        //jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
    }
}

// select one version of the Engine
ext.jmonkeyengineVersion = '3.2.1-stable' // from jcenter
//ext.jmonkeyengineVersion = '3.2.1-SNAPSHOT' // from mavenLocal

if (!hasProperty('mainClass')) {
    ext.mainClass = 'maud.Maud'
}
jar.manifest.attributes(
    'Main-Class': ext.mainClass
)

repositories {
    //mavenLocal()
    maven { url 'https://dl.bintray.com/stephengold/jme3utilities' }
    //maven { url 'https://dl.bintray.com/stephengold/org.jmonkeyengine' }
    //maven { url 'https://dl.bintray.com/jmonkeyengine/contrib' }
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    // from stephengold's bintray repositories:
    compile 'jme3utilities:jme3-utilities-heart:2.16.0'
    compile 'jme3utilities:jme3-utilities-nifty:0.8.2'
    compile 'jme3utilities:jme3-utilities-ui:0.6.6'
    compile 'jme3utilities:Minie:0.6.0'
    compile 'jme3utilities:SkyControl:0.9.14'
    compile 'jme3utilities:Wes:0.3.7'
    //runtime 'org.jmonkeyengine:jme3-testdata:3.1.0-stable'

    // from jcenter (or mavenLocal) repositories:
    compile "org.jmonkeyengine:jme3-blender:$jmonkeyengineVersion"
    compile "org.jmonkeyengine:jme3-lwjgl:$jmonkeyengineVersion"
    compile "org.jmonkeyengine:jme3-plugins:$jmonkeyengineVersion"
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'com.atr:jme-ttf:2.1.2'
    runtime 'com.github.nifty-gui:nifty-style-black:1.4.2'

    // from jitpack repositories:
    compile 'com.github.xbuf.jme3_xbuf:jme3_xbuf_loader:0.9.1'
}

// cleanup tasks
clean { dependsOn 'cleanDLLs', 'cleanSOs', 'cleanWrittenAssets' }
task cleanDLLs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dll')
}
task cleanSOs(type: Delete) {
    delete fileTree(dir: '.', include: '*.so')
}
task cleanWrittenAssets(type: Delete) {
    delete 'Written Assets'
}