plugins {
    id 'application'
    id 'java'
}

description = 'an editor for the animated 3-D models'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

tasks.withType(JavaCompile) { // compile-time options:
    options.compilerArgs << '-Xdiags:verbose'
    options.compilerArgs << '-Xlint:unchecked'
    //options.deprecation = true
    options.encoding = 'UTF-8'
}

tasks.withType(JavaExec) { // runtime options:
    //args '--forceDialog'
    //args '--openGL3'
    //args '--openGL33'
    //args '--skipStartup'
    //args '--verbose'
    classpath sourceSets.main.runtimeClasspath
    //debug true
    enableAssertions true
    //jvmArgs '-verbose:gc'
    //jvmArgs '-Xbatch'
    jvmArgs '-Xms4g', '-Xmx4g'
    //jvmArgs '-XX:+PrintCompilation'
    //jvmArgs '-XX:+UseConcMarkSweepGC'
    jvmArgs '-XX:+UseG1GC', '-XX:MaxGCPauseMillis=10'
}

ext.jmonkeyengineVersion = '3.3.2-stable' // from jcenter

mainClassName = 'maud.Maud'
if (!hasProperty('mainClass')) {
    ext.mainClass = mainClassName
}
jar.manifest.attributes('Main-Class': mainClassName)

repositories {
    //mavenLocal()
    //maven { url 'https://dl.bintray.com/stephengold/org.jmonkeyengine' } // for jme3-testdata
    //maven { url 'https://dl.bintray.com/jmonkeyengine/contrib' }
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    // from jcenter (or mavenLocal) repositories:
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation 'com.atr:jme-ttf:2.2.2'
    runtimeOnly    'com.github.nifty-gui:nifty-style-black:1.4.3'
    implementation 'com.github.stephengold:Heart:6.0.0'
    implementation 'com.github.stephengold:jme3-utilities-ui:0.9.0'
    implementation 'com.github.stephengold:jme3-utilities-nifty:0.9.15'
    implementation 'com.github.stephengold:Minie:3.0.0'
    implementation 'com.github.stephengold:SkyControl:0.9.27'
    implementation 'com.github.stephengold:Wes:0.6.1'
    implementation 'org.jmonkeyengine:jme3-blender:' + jmonkeyengineVersion
    implementation 'org.jmonkeyengine:jme3-desktop:' + jmonkeyengineVersion
    implementation 'org.jmonkeyengine:jme3-lwjgl:' + jmonkeyengineVersion
    implementation 'org.jmonkeyengine:jme3-plugins:' + jmonkeyengineVersion

    // from jitpack repositories:
    implementation 'com.github.xbuf.jme3_xbuf:jme3_xbuf_loader:0.9.1'

    // from stephengold's bintray repositories:
    //runtimeOnly 'org.jmonkeyengine:jme3-testdata:3.1.0-stable'
}

// cleanup tasks
clean.dependsOn('cleanDLLs', 'cleanDyLibs', 'cleanLogs', 'cleanSOs', 'cleanWrittenAssets')
task cleanDLLs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dll')
}
task cleanDyLibs(type: Delete) {
    delete fileTree(dir: '.', include: '*.dylib')
}
task cleanLogs(type: Delete) {
    delete fileTree(dir: '.', include: 'hs_err_pid*.log')
}
task cleanSOs(type: Delete) {
    delete fileTree(dir: '.', include: '*.so')
}
task cleanWrittenAssets(type: Delete) {
    delete 'Written Assets'
}
